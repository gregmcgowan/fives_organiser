buildscript {
    ext.versions = [
            'android_gradle'      : '4.1.0',
            'androidx'            : [
                    'appcompat'        : '1.2.0',
                    'card_view'        : '1.0.0',
                    'core'             : '2.0.0',
                    'constraint_layout': '1.1.3',
                    'fragment'         : '1.0.0',
                    'lifecycle'        : '2.2.0',
                    'palette'          : '1.0.0',
                    'recyclerview'     : '1.1.0',
                    'test_runner'      : '1.1.0',
                    'test_core'        : '1.0.0',
                    'test_rules'       : '1.1.0',
                    'test_junit'       : '1.0.0',
                    'arch_core_testing': "1.0.0"
            ],
            'kotlin'              : '1.4.10',
            'kotlin_gradle_plugin': '1.4.10',
            'coroutines'          : '1.3.7',
            'material'            : '1.0.0',
            'firebase_core'       : '16.0.9',
            'firebase_auth'       : '17.0.0',
            'firebase_database'   : '16.0.3',
            'firestore'           : '17.1.1',
            'crashlytics'         : '2.9.7',
            'fabric_gradle'       : '1.26.1',
            'dagger'              : '2.16', // Pinning this due to https://issuetracker.google.com/115738511
            'google_services'     : '4.2.0',
            'threetenabp'         : '1.1.0',
            'timber'              : '4.7.1',
            'espresso'            : '3.1.0-alpha4',
            'junit'               : '4.12',
            'mockito'             : '2.23.0',
            'mockito_kotlin'      : '2.0.0-RC3',
            'hamcrest'            : '1.3',
            'robolectric'         : '4.0.1',
            'detek'               : '1.0.0.RC6-4'

    ]
    ext.buildConfig = [
            'minSdk'    : 21,
            'compileSdk': 28,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_gradle_plugin"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "com.google.gms:google-services:$versions.google_services"
        classpath "io.fabric.tools:gradle:$versions.fabric_gradle"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-4"
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://maven.google.com' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

detekt {
    version = "${versions.detek}"
    profile("main") {
        input = "$projectDir/app"
        config = "$projectDir/quality/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/quality/reports"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate {
        if (pluginManager.hasPlugin("kotlin-kapt")) {
            kapt {
                useBuildCache = true
            }
        }
    }
}